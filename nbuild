#!/usr/local/bin/coffee
require 'colors'

fs           = require 'fs'
_            = require 'underscore'
path         = require 'path'
OptionParser = require('coffee-script/lib/coffee-script/optparse').OptionParser
Builder      = require('./builder')

{join, existsSync} = path

printLine = (line) -> process.stdout.write line + '\n'

VERSION = "0.1"
BANNER = 'Usage: nbuild [options] command[:step]'
SWITCHES = [
  ['-c', '--config [FILE*]',    'path to .nproj file']
  ['-e', '--enveroument [ARG]', 'set enveroument']
  ['-b', '--verbose',           'verbose output']
  ['-v', '--version',           'show version']
  ['-h', '--help',              'display this help message']
]

main = () ->
  optParser  = new OptionParser SWITCHES, BANNER
  o = optParser.parse process.argv[2..]
  
  if o.help
    printLine optParser.help() 
    return
    
  if o.version
    printLine "nbuild v#{VERSION}"
    return
  
  configFiles = []
  
  # добавляем конфиги прописанные фручную
  if o.config?
    for cfg in o.config when existsSync(cfg) 
      configFiles = _.union(configFiles, fs.realpathSync(cfg))
      
  if configFiles.length is 0
    # находим первый .nproj-файл в текущей директории
    for file in fs.readdirSync(__dirname) when /\.nproj$/i.test(file)
      configFiles.push fs.realpathSync(join(__dirname, file))
      break
      
  if configFiles.length is 0
    printLine "Config wasn't found, read the help for more information `nbuild -h`".yellow
    return
    
  if o.arguments.length is 0
    printLine "Command is not specified, read the help for more information `nbuild -h`".yellow
    return
    
  builderOptions = {}
  builderOptions.verbose     = o.verbose if o.verbose
  builderOptions.enveroument = o.enveroument if o.enveroument
  builderOptions.configFiles = configFiles
  
  try
    builder = new Builder(builderOptions)
    builder.run(cmd) for cmd in o.arguments
  catch err
    console.log printLine("#{err}".red)
    
main()
